plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.1")
}


dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'application'

	repositories {
		mavenCentral()
	}

	dependencies {
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	test {
		useJUnitPlatform()
	}
}

tasks.register('runAll') {
	doFirst {
		println "Starting all services..."
	}
	// 각 서브 프로젝트의 bootRun 태스크를 실행하는 설정
	subprojects.each { subproject ->
		if (subproject.plugins.hasPlugin('org.springframework.boot')) {
			dependsOn "${subproject.path}:bootRun"
		}
	}
}


configure(subprojects.findAll { it.name.endsWith('service') }) {
	bootJar {
		enabled = false
	}
	jar {
		enabled = true
	}
}
